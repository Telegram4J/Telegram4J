plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.38.0'
}

ext {
    reactor_bom_version = '2020.0.14'
    immutables_version = '2.9.0-beta3'
    findbugs_version = '3.0.1'
    javax_annotation = '1.3.2'
    junit_version = '5.7.2'
    logback_version = '1.2.3'
    caffeine_version = '3.0.5'

    tl_parser_version = '0.1.0'
    tl_parser_group = 'com.telegram4j'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.ben-manes.versions'

    group = 'com.telegram4j'
    description = 'Java library for the Telegram API'
    version = '0.1.0'

    targetCompatibility = JavaVersion.VERSION_11
    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        api platform("io.projectreactor:reactor-bom:$reactor_bom_version")

        implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"

        api "$tl_parser_group:tl-parser:$tl_parser_version"
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.incremental = true
    }

    tasks.withType(Javadoc) {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }

        options {
            encoding = "UTF-8"
            addStringOption "encoding", "UTF-8"
            tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
        }
    }

    test {
        useJUnitPlatform()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }
}

subprojects {
    archivesBaseName = "telegram4j-$project.name"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId archivesBaseName

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    name = 'Telegram4J'
                    description = 'A java REST wrapper for the Telegram Bot API'
                    url = 'https://github.com/Telegram4J/Telegram4J'
                    inceptionYear = '2021'

                    developers {
                        developer {
                            name = 'The Telegram4J'
                        }
                    }

                    licenses {
                        license {
                            name = 'GPL-3.0'
                            url = 'https://github.com/Telegram4J/Telegram4J/LICENSE'
                            distribution = 'repo'
                        }
                    }

                    scm {
                        url = 'https://github.com/Telegram4J/Telegram4J'
                        connection = 'scm:git:git://github.com/Telegram4J/Telegram4J.git'
                        developerConnection = 'scm:git:ssh://git@github.com:Telegram4J/Telegram4J.git'
                    }
                }
            }
        }
    }
}
