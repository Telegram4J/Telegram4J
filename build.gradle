plugins {
    id 'java-library'
    id 'maven-publish'
}

ext {
    reactor_bom_version = '2020.0.13'
    immutables_version = '2.9.0-beta3'
    findbugs_version = '3.0.1'
    javax_annotation = '1.3.2'
    junit_version = '5.7.2'
    logback_version = '1.2.3'

    tl_parser_version = 'master-SNAPSHOT'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'com.telegram4j'
    description = 'Java library for the Telegram API'
    version = '0.1.0'

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        api platform("io.projectreactor:reactor-bom:$reactor_bom_version")

        api "com.github.telegram4j:tl-parser:$tl_parser_version"
        api "com.github.telegram4j.tl-parser:api:$tl_parser_version"
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.incremental = true
    }

    tasks.withType(Javadoc) {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    test {
        useJUnitPlatform()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }
}

subprojects {
    archivesBaseName = "telegram4j-$project.name"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId archivesBaseName

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    name = 'Telegram4J'
                    description = 'A java REST wrapper for the Telegram Bot API'
                    url = 'https://github.com/Telegram4J/Telegram4J'
                    inceptionYear = '2021'

                    developers {
                        developer {
                            name = 'The Telegram4J'
                        }
                    }

                    licenses {
                        license {
                            name = 'GPL-3.0'
                            url = 'https://github.com/Telegram4J/Telegram4J/LICENSE'
                            distribution = 'repo'
                        }
                    }

                    scm {
                        url = 'https://github.com/Telegram4J/Telegram4J'
                        connection = 'scm:git:git://github.com/Telegram4J/Telegram4J.git'
                        developerConnection = 'scm:git:ssh://git@github.com:Telegram4J/Telegram4J.git'
                    }
                }
            }
        }
    }
}
